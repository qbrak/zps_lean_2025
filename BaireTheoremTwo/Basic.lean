import Mathlib.Topology.GDelta.Basic
import Mathlib.Topology.MetricSpace.Basic
import Mathlib.Topology.Basic

import Mathlib.Topology.Sequences
import Mathlib.Topology.Closure
import Mathlib.Topology.MetricSpace.Pseudo.Defs
import Mathlib.Topology.Metrizable.CompletelyMetrizable
import Mathlib.Topology.UniformSpace.Cauchy


set_option linter.style.longLine false
noncomputable section

open scoped Topology
open Filter Set TopologicalSpace

variable {Œ± X Z B B1 : Type*} {Œπ : Sort*}

section BaireTheoremTwo

variable [TopologicalSpace Œ±] [BaireSpace Œ±]
variable [MetricSpace X] [CompleteSpace X] [IsCompletelyMetrizableSpace X]
--variable [MetricSpace Z]

/--
Potentially could simplified with Dense.exists_mem_open which is:
  (hs : Dense s) {U : Set X} (ho : IsOpen U) (hne : U.Nonempty) : ‚àÉ x ‚àà s, x ‚àà U
-/
theorem dense_set_intersect_open_nonempty {U : Set Œ±} {G : Set Œ±}
     (hU : IsOpen U) (hne : U.Nonempty) (hd : Dense G) :
    (U ‚à© G).Nonempty := by
  by_contra h_empty

  have UsubGc : U ‚äÜ G·∂ú := by
    rw [Set.subset_def]
    intro x hxU hxG
    have xin_inter : x ‚àà U ‚à© G := Set.mem_inter hxU hxG
    have h_nonempty : (U ‚à© G).Nonempty := ‚ü®x, xin_inter‚ü©
    contradiction

  have Uc_closed : IsClosed U·∂ú := by
    rw [<-compl_compl U] at hU
    exact isOpen_compl_iff.mp hU

  have GsubUc: G ‚äÜ U·∂ú := by
    exact Set.subset_compl_comm.mp UsubGc

  have Gclosure_subUc: closure G ‚äÜ U·∂ú := by
    exact (IsClosed.closure_subset_iff Uc_closed).mpr GsubUc
  have Uc_neq_univ : U·∂ú ‚â† univ := by
    exact Set.compl_ne_univ.mpr hne

  have Uc_sub_univ : U·∂ú ‚äÇ univ := by
    exact Set.ssubset_univ_iff.mpr Uc_neq_univ

  have Gclosure_sub_univ : closure G ‚äÇ univ := by
    exact Set.ssubset_of_subset_of_ssubset Gclosure_subUc Uc_sub_univ

  have Gclosure_neq_univ : closure G ‚â†  univ := by
    exact Set.ssubset_univ_iff.mp Gclosure_sub_univ

  have Gclosure_eq_univ : closure G = univ := by
    exact Dense.closure_eq hd

  contradiction

-- theorem dense_set_intersect_open_nonempty_v2 {U : Set Œ±} {G : Set Œ±}
   --  (hU : IsOpen U) (hne : U.Nonempty) (hd : Dense G) :
    -- (U ‚à© G).Nonempty := by
    -- sorry
  -- rw [Set.nonempty_def]
  -- rw [Set.mem_inter_iff x U G]
  -- rw [Dense.exists_mem_open hd hU hne] at hne


theorem set_dense_iff_intersect_open_nonempty {s : Set X} :
    Dense s ‚Üî ‚àÄ (U : Set X), IsOpen U ‚Üí U.Nonempty ‚Üí (U ‚à© s).Nonempty := by
  exact dense_iff_inter_open

lemma exist_open_ball_smaller_radius_subset {G : Set X} {r : ‚Ñù} (hr : 0 < r) (U : Set X)
    (hUopen : IsOpen U) (hUne : U.Nonempty) (hGOpen : IsOpen G) (hGDense : Dense G) :
    ‚àÉ (x : X) (r2 : ‚Ñù), r2 > 0 ‚àß (Metric.closedBall x r2 ‚äÜ U ‚à© G) ‚àß r2 < r := by
    have h_inter_nonempty : (U ‚à© G).Nonempty := by
      exact set_dense_iff_intersect_open_nonempty.mp hGDense U hUopen hUne
    have open_inter : IsOpen (U ‚à© G) := by
      apply IsOpen.inter hUopen hGOpen
    rw [nonempty_def] at h_inter_nonempty
    have exists_any_ball : ‚àÉ (f : X) (g: ‚Ñù), f ‚àà U ‚à© G ‚àß g > 0
    ‚àß Metric.ball f g ‚äÜ U ‚à© G := by
      rcases h_inter_nonempty with ‚ü®x0, hx0‚ü©
      use x0
      rcases Metric.isOpen_iff.mp open_inter x0 hx0 with ‚ü®Œµ, hŒµ_pos, h_ball_subset‚ü©
      use Œµ
    rcases exists_any_ball with ‚ü®x0, g1, hx0, hg1, hg2‚ü©
    use x0
    use min (g1/2) (r / 2)
    constructor
    ¬∑ apply lt_min (half_pos hg1) (half_pos hr)
    ¬∑ constructor
      ¬∑ apply Subset.trans (Metric.closedBall_subset_ball
      (lt_of_le_of_lt (min_le_left (g1/2) (r/2)) (half_lt_self (hg1)))) hg2
      ¬∑ exact min_lt_of_right_lt (half_lt_self hr)


lemma exists_nested_balls_sequence {U : Set X} {G : ‚Ñï ‚Üí Set X}
(hUopen : IsOpen U) (hUne : U.Nonempty) (hGopen : ‚àÄ n, IsOpen (G n)) (hGdense : ‚àÄ n, Dense (G n)) :
‚àÉ (r: ‚Ñï ‚Üí ‚Ñù) (x: ‚Ñï ‚Üí X), (‚àÄ n, Metric.closedBall (x (n+1)) (r (n+1)) ‚äÜ (Metric.ball (x n) (r n) ‚à© G n))
‚àß (Metric.closedBall (x 0) (r 0) ‚äÜ U ‚à© G 0) ‚àß (‚àÄ n, 0 < r n ‚àß  r n ‚â§ 1/2^n) := by


  -- 1. krok bazowyy (n=0)
  -- szukamy kuli w U ‚à© G 0 o promieniu mniejszym niz 1
  have exists_zero : ‚àÉ (x : X) (r : ‚Ñù), r > 0 ‚àß Metric.closedBall x r ‚äÜ U ‚à© G 0 ‚àß r < 1 := by
    apply exist_open_ball_smaller_radius_subset zero_lt_one U hUopen hUne (hGopen 0) (hGdense 0)
  -- rozpakowuje pieknie istnienie kuli poczatkowej
  rcases exists_zero with ‚ü®x0, r0, hr0_pos, h0_sub, hr0_lt‚ü©

  -- 2. krok rekurencyjny
  -- dla ka≈ºdej kuli (xn, rn) istnieje mniejsza kula wewnƒÖtrz niej przeciƒôtej z G n

  have exists_next : ‚àÄ (n : ‚Ñï) (xn : X) (rn : ‚Ñù),
      rn > 0 ‚Üí ‚àÉ xn_next rn_next, rn_next > 0 ‚àß
      Metric.closedBall xn_next rn_next ‚äÜ Metric.ball xn rn ‚à© G n ‚àß
      rn_next < 1 / 2 ^ (n + 1) := by

    intro n xn rn hrn_pos
    let Un := Metric.ball xn rn
    have hUn_open : IsOpen Un := Metric.isOpen_ball
    have hUn_ne : Un.Nonempty := Metric.nonempty_ball.mpr hrn_pos
    -- Promie≈Ñ musi byƒá mniejszy ni≈º 1/2^(n+1)
    have h_rad_limit_pos : 0 < (1 / 2 : ‚Ñù) ^ (n + 1) := by
      simp;

    -- U≈ºywamy lematu pomocniczego dla kuli Un i zbioru gƒôstego G n
    rcases exist_open_ball_smaller_radius_subset h_rad_limit_pos Un hUn_open hUn_ne (hGopen n) (hGdense n)
      with ‚ü®xn_next, rn_next, hrn_next_pos, h_sub, h_rad_lt‚ü©

    use xn_next, rn_next
    constructor
    ¬∑ exact hrn_next_pos
    ¬∑ constructor
      ¬∑ exact h_sub
      ¬∑ simp
        simp at h_rad_lt
        exact h_rad_lt
  -- 3. Zbieramy wszystko razem u≈ºywajƒÖc zasady wyboru (!!dopisac do wstepu)
  choose! x r h_next using exists_next
 -- let rec xr_seq : ‚Ñï ‚Üí X √ó ‚Ñù := fun n => if n = 0 then (x0, r0)
 --   else (x n (xr_seq (n - 1)).1 (xr_seq (n - 1)).2, r n (xr_seq (n - 1)).1 (xr_seq (n - 1)).2)
  let xr_seq : ‚Ñï ‚Üí X √ó ‚Ñù := fun n =>
    Nat.recOn n (x0, r0) (fun n' prev => (x n' prev.1 prev.2, r n' prev.1 prev.2))
  let x_seq : ‚Ñï ‚Üí X := fun n => (xr_seq n).1
  let r_seq : ‚Ñï ‚Üí ‚Ñù := fun n => (xr_seq n).2
  have r_seq_pos : ‚àÄ n, r_seq n > 0 := by
    intro n
    induction n with
     -- przypadek bazowy n=0
      | zero => dsimp [r_seq]
                exact hr0_pos
      | succ k ih => dsimp [r_seq]
                     dsimp [xr_seq]
                     apply (h_next k (x_seq k) (r_seq k) (ih)).1


  use r_seq, x_seq
  constructor
  ¬∑ intro n
    induction n with
     -- przypadek bazowy n=0
      | zero => rw [zero_add]
                dsimp [x_seq, r_seq]
                dsimp [xr_seq]
                exact (h_next 0 x0 r0 hr0_pos).2.1
      | succ k ih => --rw [Nat.add_succ]
                     dsimp [x_seq, r_seq]
                     dsimp [xr_seq]
                     exact (h_next (k + 1) (x_seq (k+1)) (r_seq (k+1)) (r_seq_pos (k+1))).2.1

  ¬∑ constructor
    ¬∑ dsimp [x_seq, r_seq]
      dsimp [xr_seq]
      exact h0_sub

    intro n
    constructor
    ¬∑ exact r_seq_pos n

    ¬∑ dsimp [r_seq]

      induction n with
       -- przypadek bazowy n=0
        | zero => dsimp [xr_seq]
                  simp
                  exact (le_of_lt hr0_lt)
        | succ k ih =>
            have h_prev := (h_next k (x_seq k) (r_seq k) (r_seq_pos k)).2.2
            simp at h_prev
            dsimp [xr_seq]
            simp
            exact (le_of_lt h_prev)


/--
The primary form of the Baire Category Theorem:
  The countable intersection of dense open sets in a complete metric space is dense.
-/
theorem complete_metric_has_baire_property {G : ‚Ñï ‚Üí Set X} (ho : ‚àÄ n, IsOpen (G n))
  (hd : ‚àÄ n, Dense (G n))
  : Dense (‚ãÇ n, G n) := by

  rw [dense_iff_inter_open]
  intro U hUopen hUnempty
  have exists_nested_balls := exists_nested_balls_sequence hUopen hUnempty ho hd
  rcases exists_nested_balls with ‚ü®r, x, h_nested_balls‚ü©
  rcases h_nested_balls with ‚ü®h_nested, h_nested0, h_r_bound‚ü©

  have hSeqIsCauchy : ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n ‚â• N, dist (x n) (x N) < Œµ := by
    intro Œµ hŒµ

    -- find N such that r N < Œµ/2
    have exists_N : ‚àÉ N, r N < Œµ / 2 := by
      -- since r n ‚â§ 1/2^n, we can choose N large enough
      -- apply Nat.find (fun n => r n < Œµ / 2)
      -- Use the lemma that (1/2)^n can be made arbitrarily small

      rcases exists_pow_lt_of_lt_one (half_pos hŒµ) one_half_lt_one with ‚ü®N, hN‚ü©
      use N
      -- If we just skip these simps than what we get is a difference
      -- between (1 / 2) ^ N and 1 / 2 ^ N
      -- and with the simp-s we unify the form so that what we compare is the same
      simp at hN
      simp at h_r_bound
      exact LT.lt.trans_le' hN (h_r_bound N).right

    rcases exists_N with ‚ü®N, h_r_N‚ü©
    -- Instead of `refine ‚ü®N, ?_‚ü©` we can just do:
    use N
    intro n hn_ge_N

    have hn_seq_in_N_ball :
        Metric.closedBall (x n) (r n) ‚äÜ Metric.closedBall (x N) (r N) := by
      -- use the nested balls property to show x n is in the closed ball around x N
      induction n, hn_ge_N using Nat.le_induction with
      | base =>
        simp
      | succ k kgtN hk =>
        have ball_k_subset_closedBall_k : Metric.ball (x k) (r k) ‚à© G k ‚äÜ Metric.closedBall (x k) (r k) := by
          exact Set.Subset.trans (Set.inter_subset_left) Metric.ball_subset_closedBall
        exact Set.Subset.trans (
          Set.Subset.trans (h_nested k) ball_k_subset_closedBall_k
          ) hk

    have x_seq_n_in_N_ball : x n ‚àà Metric.closedBall (x N) (r N) := by
      apply hn_seq_in_N_ball
      apply Metric.mem_closedBall_self
      exact le_of_lt (h_r_bound n).left

      -- exact Set.mem_of_mem_of_subset (Metric.mem_closedBall_self (x n)) hn_seq_in_N_ball
    have x_n_dist_x_N_is_r_N :=
      Metric.mem_closedBall.mp x_seq_n_in_N_ball

    calc
      dist (x n) (x N) ‚â§ r N := x_n_dist_x_N_is_r_N
      _ < Œµ / 2 := h_r_N
      _ < Œµ := by linarith

  -- now we have shown the sequence is Cauchy
    -- use the nested balls property to show dist (x n) (x N) < Œµ

  have hSeqLimit : ‚àÉ x_lim : X, Tendsto (fun n => x n) atTop (ùìù x_lim) := by
    -- 1. Convert the metric epsilon-N property to the formal CauchySeq property
    have h_cauchy : CauchySeq x := Metric.cauchySeq_iff'.mpr hSeqIsCauchy
    -- 2. Use the fact that X is a CompleteSpace to show it converges
    exact cauchySeq_tendsto_of_complete h_cauchy

  rcases hSeqLimit with ‚ü®x_lim, h_tendsto‚ü©

  have x_lim_in_balls : ‚àÄ (n : ‚Ñï), x_lim ‚àà Metric.closedBall (x n) (r n) := by
    intro n

    have ball_m_subset_ball_n : ‚àÄ m ‚â• n, Metric.closedBall (x m) (r m) ‚äÜ Metric.closedBall (x n) (r n) := by
      intro m mgt_n
      induction m, mgt_n using Nat.le_induction with
      | base =>
        exact subset_rfl
      | succ i hi_ge_n hi_subset =>
        -- Wiemy z h_nested_balls, ≈ºe B_{i+1} ‚äÜ B_i
        have step_nested := (h_nested i)
        -- B_{i+1} ‚äÜ ball(i) ‚äÜ closedBall(i)
        have step_sub : Metric.closedBall (x (i + 1)) (r (i + 1)) ‚äÜ Metric.closedBall (x i) (r i) := by
          exact Subset.trans step_nested (Subset.trans Set.inter_subset_left Metric.ball_subset_closedBall)
        -- Z przechodnio≈õci: B_{i+1} ‚äÜ B_i ‚äÜ B_n
        exact Subset.trans step_sub hi_subset

    have x_k_in_ball_x_n : ‚àÄ k ‚â• n, x k ‚àà Metric.closedBall (x n) (r n) := by
      intro k hk_ge_n
      induction k, hk_ge_n using Nat.le_induction with
      | base =>
        exact Metric.mem_closedBall_self (le_of_lt ((h_r_bound n).left))
      | succ m mgt_n hm_ind =>
        -- 1. Teraz pokazujemy, ≈ºe x_{m+1} le≈ºy w kuli m-tej
        have x_next_in_ball_m : x (m + 1) ‚àà Metric.closedBall (x m) (r m) := by
          -- Bierzemy w≈Çasno≈õƒá zagnie≈ºd≈ºenia dla m: B_{m+1} ‚äÜ B_m
          have step := (h_nested m)
          -- x_{m+1} jest ≈õrodkiem B_{m+1}, wiƒôc w niej le≈ºy
          have center_in_self : x (m + 1) ‚àà Metric.closedBall (x (m + 1)) (r (m + 1)) :=
            Metric.mem_closedBall_self (le_of_lt (h_r_bound (m + 1)).left)

          -- Skoro jest w B_{m+1}, a B_{m+1} ‚äÜ B_m, to jest w B_m
          apply (Subset.trans step (Subset.trans Set.inter_subset_left Metric.ball_subset_closedBall))
          exact center_in_self

        -- 2. Konkluzja: Skoro x_{m+1} ‚àà B_m, a B_m ‚äÜ B_n, to x_{m+1} ‚àà B_n
        exact (ball_m_subset_ball_n m mgt_n) x_next_in_ball_m

    -- have h_closed := Metric.isClosed_ball (x := x n) (Œµ := r n)
    have ball_seq_closed : IsSeqClosed (Metric.closedBall (x n) (r n)) :=
      IsClosed.isSeqClosed (Metric.isClosed_closedBall (x := x n) ( Œµ := r n))

    exact IsClosed.mem_of_tendsto
      (Metric.isClosed_closedBall (x := x n) (Œµ := r n))
      h_tendsto
      (Filter.eventually_atTop.mpr ‚ü®n, x_k_in_ball_x_n‚ü©)

  have x_lim_in_G_n : ‚àÄ n, x_lim ‚àà G n := by
    intro n
    have h_ball_np1_sub_G_n : ‚àÄ (n : ‚Ñï ), Metric.closedBall (x (n+1)) (r (n+1)) ‚äÜ G n := by
      intro n
      exact (Set.subset_inter_iff.mp (h_nested n)).right
    exact Set.mem_of_mem_of_subset (x_lim_in_balls (n+1)) (h_ball_np1_sub_G_n n)

  have x_lim_in_U : x_lim ‚àà U := by
    have h_lim_in_ball_0 : x_lim ‚àà Metric.closedBall (x 0) (r 0) := by
      exact x_lim_in_balls 0
    have ball_0_subset_U : Metric.closedBall (x 0) (r 0) ‚äÜ U  := by
      have ball_0_subset_U_cap_G_0 : Metric.closedBall (x 0) (r 0) ‚äÜ U ‚à© G 0 := by
        exact h_nested0
      simp at ball_0_subset_U_cap_G_0
      exact ball_0_subset_U_cap_G_0.left
    exact Set.mem_of_mem_of_subset h_lim_in_ball_0 ball_0_subset_U

  have x_lim_in_inter : x_lim ‚àà U ‚à© ‚ãÇ n, G n := by
    simp_all
  rw [Set.nonempty_def]
  use x_lim

/--
The second form of the Baire Category Theorem:
  In a complete metric space, the union of countably many
  nowhere dense sets cannot be the whole space.
-/
theorem sum_sets_atleast_one_not_nowhere_dense {G : ‚Ñï ‚Üí Set X} [Nonempty X]
   (hUnion : ‚ãÉ n, G n = univ) :
    ‚àÉ n, ¬¨ IsNowhereDense (G n) := by
  by_contra h_contra
  push_neg at h_contra
  have compl_closure_open : ‚àÄ n, IsOpen (closure (G n))·∂ú := by
    simp
  have compl_closure_dense : ‚àÄ n, Dense (closure (G n))·∂ú := by
    intro n
    rw [dense_iff_inter_open]
    intro U hUopen hUnempty
    by_contra hcontraEmpty
    push_neg at hcontraEmpty
    have hcontraFull : (U ‚à© (closure (G n))·∂ú)·∂ú = (‚àÖ)·∂ú := by
      simp
      exact hcontraEmpty
    rw [Set.compl_inter] at hcontraFull
    rw [Set.compl_empty] at hcontraFull
    rw [compl_compl] at hcontraFull

    have hUcClosed : IsClosed U·∂ú := by
      have hUccOpen : IsOpen U·∂ú·∂ú := by
        rw [compl_compl]
        exact hUopen
      exact isOpen_compl_iff.mp hUccOpen

    have hlessThanSpace : interior (closure (G n) ‚à™ U·∂ú) ‚äÜ  U·∂ú := by
      have hlessThanUc : interior (closure (G n) ‚à™ U·∂ú) ‚äÜ interior (closure (G n)) ‚à™ U·∂ú:= by
        exact  IsClosed.interior_union_right hUcClosed
      rw [h_contra n] at hlessThanUc
      rw [Set.empty_union] at hlessThanUc
      exact hlessThanUc

    have hUc_ne_univ : U·∂ú ‚â† univ := by
      rw [Set.compl_ne_univ]
      exact hUnempty
    have hUc_sub_univ : U·∂ú ‚äÇ univ := by
      exact Set.ssubset_univ_iff.mpr hUc_ne_univ
    rw [Set.union_comm] at hcontraFull

    have intNotUniv : interior (closure (G n) ‚à™ U·∂ú) ‚äÇ univ := by
      exact ssubset_of_subset_of_ssubset hlessThanSpace hUc_sub_univ
    have intNotUnivNeq : interior (closure (G n) ‚à™ U·∂ú) ‚â† univ := by
      exact Set.ssubset_univ_iff.mp intNotUniv
    have intEqUniv : interior (closure (G n) ‚à™ U·∂ú) = interior (univ) := by
      simpa

    rw [interior_univ] at intEqUniv
    contradiction

  have compl_closure_inter_dense : Dense (‚ãÇ n, (closure (G n))·∂ú) := by
    exact complete_metric_has_baire_property compl_closure_open compl_closure_dense
  have compl_closure_inter_eq : (‚ãÇ n, (closure (G n))·∂ú) = (‚ãÉ n, closure (G n))·∂ú := by
    simp
  rw [compl_closure_inter_eq] at compl_closure_inter_dense
  have int_eq_empty : interior (‚ãÉ n, (closure (G n))) = ‚àÖ := by
    exact interior_eq_empty_iff_dense_compl.mpr compl_closure_inter_dense

  have union_closure_eq_space : (‚ãÉ n, closure (G n)) = univ := by
    have union_closure_sub_univ : (‚ãÉ n, closure (G n)) ‚äÜ univ := by
      simp
    have univ_sub_union_closure : univ ‚äÜ (‚ãÉ n, closure (G n)) := by
      have univ_sub_union : univ ‚äÜ (‚ãÉ n, G n) := by
        exact Set.univ_subset_iff.mpr hUnion
      have union_sub_union_closure : (‚ãÉ n, G n) ‚äÜ (‚ãÉ n, closure (G n)) := by
        simp
        have G_in_closure : ‚àÄ n, G n ‚äÜ closure (G n) := by
          simp [subset_closure]

        intro n
        have closure_sub : closure (G n) ‚äÜ (‚ãÉ n, closure (G n)) := by
          have set_same_subset : closure (G n) ‚äÜ (closure (G n)) := by
            simp
          exact Set.subset_iUnion_of_subset n set_same_subset
        exact subset_trans (G_in_closure n) closure_sub
      exact subset_trans univ_sub_union union_sub_union_closure

    -- The ‚ü®a,b‚ü© means (a and b)
    exact Set.Subset.antisymm_iff.mpr ‚ü®union_closure_sub_univ, univ_sub_union_closure‚ü©

  rw [union_closure_eq_space] at int_eq_empty
  rw [interior_univ] at int_eq_empty
  simp_all


theorem dense_iInter_of_isOpen_nat2 {G : ‚Ñï ‚Üí Set Œ±} (ho : ‚àÄ n, IsOpen (G n))
    (hd : ‚àÄ n, Dense (G n)) : Dense (‚ãÇ n, G n) :=
  BaireSpace.baire_property G ho hd



end BaireTheoremTwo
